CMAKE_MINIMUM_REQUIRED(VERSION 3.9)

PROJECT(mfmg LANGUAGES CXX VERSION ${MFMG_VERSION})

SET(CMAKE_MODULE_PATH ${CMAKE_SOURCE_DIR}/cmake)
INCLUDE(SetupTPLs)
INCLUDE(SetupMFMG)

IF(${MFMG_ENABLE_COVERAGE})
  INCLUDe(CodeCoverage)
ENDIF()

INCLUDE(Testing)
ENABLE_TESTING()
INCLUDE(CTest)

INCLUDE_DIRECTORIES(include)
FILE(GLOB MFMG_HEADERS include/*.hh)

ADD_SUBDIRECTORY(source)

IF (${MFMG_ENABLE_TESTS})
  ADD_SUBDIRECTORY(tests)
ENDIf()

# Provide "indent" target for indenting all the header and the source files.
ADD_CUSTOM_TARGET(indent
  WORKING_DIRECTORY ${CMAKE_SOURCE_DIR}
  COMMAND ./indent
  )

IF(${MFMG_ENABLE_DOCUMENTATION})
  ADD_SUBDIRECTORY(doc)
ENDIF()

IF (${MFMG_ENABLE_SHARED})
  ADD_LIBRARY(mfmg SHARED ${MFMG_SOURCES} ${MFMG_HEADERS})
ELSE()
  ADD_LIBRARY(mfmg ${MFMG_SOURCES} ${MFMG_HEADERS})
ENDIF()
TARGET_INCLUDE_DIRECTORIES(mfmg SYSTEM PUBLIC ${MPI_CXX_INCLUDE_PATH})
TARGET_LINK_LIBRARIES(mfmg PUBLIC ${MPI_CXX_LIBRARIES})
TARGET_INCLUDE_DIRECTORIES(mfmg SYSTEM PUBLIC ${Boost_INCLUDE_DIRS})
TARGET_INCLUDE_DIRECTORIES(mfmg SYSTEM PUBLIC ${DEAL_II_INCLUDE_DIRS})
TARGET_LINK_LIBRARIES(mfmg PUBLIC ${DEAL_II_LIBRARIES})


SET_TARGET_PROPERTIES(mfmg PROPERTIES
  CXX_STANDARD 11
  CXX_STANDARD_REQUIRED ON
  VERSION ${MFMG_VERSION}
  )

# Install the library
INSTALL(TARGETS mfmg 
  ARCHIVE DESTINATION ${CMAKE_INSTALL_PREFIX}/lib
  LIBRARY DESTINATION ${CMAKE_INSTALL_PREFIX}/lib)
# Install the header files
INSTALL(DIRECTORY include DESTINATION ${CMAKE_INSTALL_PREFIX})

# Create the config file
CONFIGURE_FILE(cmake/mfmgConfig.cmake.in
  ${CMAKE_BINARY_DIR}/cmake/mfmgConfig.cmake
  @ONLY
  )

# Install the config file
INSTALL(FILES ${CMAKE_BINARY_DIR}/cmake/mfmgConfig.cmake
  DESTINATION ${CMAKE_INSTALL_PREFIX}/lib/cmake/mfmg
  )
